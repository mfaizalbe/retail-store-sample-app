locals {
  security_groups_active = var.pod_security_groups_enabled
  environment_name       = terraform.workspace == "default" ? var.environment_name : "${var.environment_name}-${terraform.workspace}"
}

module "tags" {
  source = "../../lib/tags"

  environment_name = local.environment_name
}

module "vpc" {
  source = "../../lib/vpc"

  environment_name = local.environment_name

  public_subnet_tags = {
    "kubernetes.io/cluster/${local.environment_name}" = "shared"
    "kubernetes.io/role/elb"                          = 1
  }

  private_subnet_tags = {
    "kubernetes.io/cluster/${local.environment_name}" = "shared"
    "kubernetes.io/role/internal-elb"                 = 1
  }

  tags = module.tags.result
}

module "dependencies" {
  source = "../../lib/dependencies"

  environment_name = local.environment_name
  tags             = module.tags.result

  vpc_id     = module.vpc.inner.vpc_id
  subnet_ids = module.vpc.inner.private_subnets

  catalog_security_group_id  = local.security_groups_active ? aws_security_group.catalog.id : module.retail_app_eks.node_security_group_id
  orders_security_group_id   = local.security_groups_active ? aws_security_group.orders.id : module.retail_app_eks.node_security_group_id
  checkout_security_group_id = local.security_groups_active ? aws_security_group.checkout.id : module.retail_app_eks.node_security_group_id
}

module "retail_app_eks" {
  source = "../../lib/eks"

  providers = {
    aws                = aws
    helm               = helm
    kubernetes.cluster = kubernetes.cluster
    kubernetes.addons  = kubernetes.addons
  }

  environment_name         = local.environment_name
  cluster_version          = "1.34"
  node_group_instance_type = var.node_group_instance_type
  vpc_id                   = module.vpc.inner.vpc_id
  vpc_cidr                 = module.vpc.inner.vpc_cidr_block
  subnet_ids               = module.vpc.inner.private_subnets
  opentelemetry_enabled    = var.opentelemetry_enabled
  tags                     = module.tags.result

  istio_enabled = var.istio_enabled
}

resource "kubectl_manifest" "load_generator" {
  yaml_body = file("${path.module}/../../../src/load-generator/deployment.yaml")

  depends_on = [
    helm_release.ui
  ]
}

